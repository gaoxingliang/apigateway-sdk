plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

apply plugin: 'maven-publish'

group 'io.github.gaoxingliang:apigateway'
version '0.0.4'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenLocal()
    mavenCentral()
}

dependencies {
    // encryption
    implementation 'cn.hutool:hutool-all:5.8.23'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.69'

    implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.14.0'

    implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    implementation group: 'com.alibaba.fastjson2', name: 'fastjson2', version: '2.0.33'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation 'com.konghq:unirest-java:3.11.09:standalone'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId =  "io.github.gaoxingliang"
            artifactId = 'apigateway-sdk'

            from components.java

            pom {
                name = 'apigateway-sdk'
                description = '四川征信api网关的示例sdk。'
                url = 'https://github.com/gaoxingliang/apigateway-sdk'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'gaoxingliang'
                        name = 'gaoxingliang'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gaoxingliang/apigateway-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/gaoxingliang/apigateway-sdk.git'
                    url = 'https://github.com/gaoxingliang/apigateway-sdk.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
        }
    }
}
